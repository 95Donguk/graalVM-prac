plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.graalvm.buildtools.native' version '0.10.2'
}

group = 'kr.co.mz'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.openjdk.jmh:jmh-core:1.37'
    implementation 'org.graalvm.buildtools:native-gradle-plugin:0.10.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// 네이티브 이미지 플러그인 설정
graalvmNative {
    // 네이티브 이미지 생성을 위한 바이너리 설정
    binaries {
        // 주 바이너리 설정(주 실행 파일)
        main {
            // 네이티브 이미지 생성 또는 실패시 오류 보고
            buildArgs.add('--no-fallback')
            // 지정한 패키지 및 클래스를 네이티브 이미지 생성시 초기화 합니다. (런타임 초기화 방지)
            buildArgs.add('--initialize-at-build-time=org.openjdk.jmh')
            // 네이티브 이미지 생성시 필요한 리소스 설정 파일의 위치를 지정합니다.
            buildArgs.add('-H:ResourceConfigurationFiles=src/main/resources/resource-config.json')
        }
    }
}

// 네이티브 이미지 컴파일이 시작하기 전에 위 설정한 리소스 설정 파일의 위치를 찾을 수 있도록 파일 복사 작업
task copyResourceConfig(type: Copy) {
    from 'src/main/resources'
    into "${buildDir}/native/nativeCompile/src/main/resources"
    include 'resource-config.json'
}

tasks.named('nativeCompile') {
    dependsOn copyResourceConfig
}